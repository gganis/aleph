      SUBROUTINE YVPOSS(IENT,NVX,NHX,NEU,
     &  VXIN,VVXIN,
     &  IXHX,NSHX,NSVHX,HXI,VHXI,
     &  IXNU,NSNU,NSVNU,TNU,VTNU,
     &  JTREQ,MULIN,MNMUL,MXMUL,NEXCL,NSNGL,KSNGL)
C
C----------------------------------------------------------*
C!    finds all combinations of tracks that form a vertex
CKEY YTOP VERTEX
C!    Author :     G. Lutz   30/11/87
C!    Modified :   M. Bosman 01/12/88
C!    MODIFIED :   G.LUTZ  FEBRUARY 1991
C!
C!
C!    Description
C!    ===========
C!    finds all combinations of tracks that form a vertex
C!
C!  INPUT:
C!    IENT .........=1 .... GENERATE TRACK CROSSING TABLE ONLY
C!                  =2 .... FIND VERTICES WITHOUT CALC. OF CROSS TABLE
C!                  =3 .... GENERATE CROSS TABLE + VERTICES
C!    NVX ......... NUMBER INPUT VERTICES (USUALLY 1 OR ZERO)
C!    NHX ......... NUMBER OF HELIX INDICES
C!    NEU ......... NUMBER OF NEUTRAL TRACKS
C!    VXIN(3,I) ... X,Y,Z OF INPUT VERTEX I
C!    VVXIN(6,I) .. VARIANCE OF INPUT VERTEX I
C!    IXHX ........ HELIX INDICES FOR VERTEX FIT
C!                  ALSO ALLOWED TO BE ZERO; THEN IT IS IGNORED
C!    NSHX ........ SPACING BETWEEN CONSECUTIVE HELIX PAR.
C!    NSVHX ....... SPACING BETWEEN CONSECUTIVE HELIX VAR.
C!    HXI(I) ...... HELIX PARAMETERS
C!    VHXI(I) ..... HELIX VARIANCES
C!    IXNU ........ NUMBER OF NEUTRAL TR. INDICES FOR VERTEX FIT
C!    NSNU ........ SPACING BETWEEN CONSECUTIVE NEUTRAL TR. PAR.
C!    NSVNU ....... SPACING BETWEEN CONSECUTIVE NEUTRAL TR. VAR.
C!    TNU(I) ...... NEUTRAL TRACK PARAMETERS
C!    VTNU(I) ..... NEUTRAL TRACK VARIANCES
C!      JTREQ(1) ... # OF SETS OF TRACKS
C!            (2) ... NEGATIVE OF # OF TRACKS REQUIRED FROM FIRST SET
C!            (3,..)  TRACK INDICES IN FIRST SET; ZERO INDICES ARE SKIPP
C!            (...)   REPEAT FROM (2) FOR OTHER SETS
C!         MULIN ... STOP SEARCHING VERTICES IF MULTIPL.
C!                    IS HIGHEST FOUND - MULIN
C!      MNMUL ....  MINIMUM MULTIPLICITY OF VERTICES (DEFAULT=2)
C!      MXMUL ....  MAXIMUM MULTIPLICITY OF VERTICES (DEFAULT NPTR)
C!     NEXCL ....     NUMBER OF EXCLUDED TRACK COMBINATIONS
C!  OUTPUT:
C!     NSNGL ........ # OF TRKS FOUND WITHOUT CROSSING ANY (REQUIRED)
C!                    OTHER TRACKS
C!     KSNGL ........ INDICES OF TRACKS WITHOUT CROSSING
C!
C----------------------------------------------------------*
#ifndef DOC
      SAVE
C
#include "bcs.h"
#include "ydimto.h"
#include "yvxpto.h"
#include "ydumto.h"
C
      DIMENSION VXIN(3,*),VVXIN(6,*)
C  HELIX PARAMETERS
      DIMENSION IXHX(*),HXI(*),VHXI(*)
C  NEUTRAL TRACK PARAMETERS
      DIMENSION IXNU(*),TNU(*),VTNU(*)
      DIMENSION JTREQ(*),KSNGL(*)
C  ORIGINAL INDEX OF SELECTED TRACKS
C DIMENSION IPTR(MAXTRK),IPTRO(MAXTRK)
      DIMENSION IPTR(MAXTRK),IPTRO(MAXTRK)
      DIMENSION IXHXX(MAXHLX),IXNUU(MAXNTR)
C DIMENSION YMKST(MKDIMM, )
      DIMENSION YMKST(MKDIMM,10),NTREQ(10),NTSET(10)
      INTEGER YMKST
C
C  TABLE FOR CROSSING PROPERTIES OF PAIRS OF TACKS
C  BIT J IN WORD I SET TO 1 IF TRACK I CROSSES TRACK J
C DIMENSION IJCRS(MKDIMM,MAXTRK) MCOMB(MKDIMM) JCOMB(MKDIMM) MMSET(MKDIM
      DIMENSION IJCRS(MKDIMM,MAXTRK),
     &      MCOMB(MKDIMM),JCOMB(MKDIMM),MMSET(MKDIMM)
C  # OF CROSSINGS OF TRACK I, # OF TRACKS WITH I CROSSINGS
C DIMENSION NJCRS(MAXTRK),KTCRS(MAXTRK)
      DIMENSION NJCRS(MAXTRK),KTCRS(MAXTRK)
C  VERTEX BETWEEN PAIRS OF TRACKS
C DIMENSION XITRV2(MAXTRK)
      DIMENSION VTX2(3),XITRV2(MAXTRK)
      DIMENSION VARV2(6)
C  ARRAY FOR INDICES OF TRACK COMBINATIONS
C DIMENSION IITR(MAXTRK),JJTR(MAXTRK),IJTR(MAXTRK)
      DIMENSION IITR(MAXTRK),JJTR(MAXTRK),IJTR(MAXTRK)
C  TEMPORARY ARRAY FOR PRINTING
C DIMENSION ITEMP(MAXTRK)
      DIMENSION ITEMP(MAXTRK)
C  FLAG TO BE SET IF ALL COMBINATIONS OF L TRACKS GIVE A POSSIBLE
C  VERTEX. FLAG IS USED FOR STOPPING FURTHER SEARCH OF VERTICES
      LOGICAL LCOMB
C  99.9% CONFIDENCE LIMITS FOR N DEGREES OF FREEDOM
      DIMENSION CONLM(20)
C     FLAG FOR RECALCULATING CROSSING TABLE
      LOGICAL LCROS
C
      LOGICAL LVAPC,LVAPP
C
      LOGICAL LMRK,LZERO
C
#if defined(YDEBUG)
      LOGICAL LDMP1,LDMP2
#endif
C
C
      DATA CONLM/10.8,13.8,16.3,18.5,20.5,22.5,24.3,26.1,27.8,29.6,
     1            31.3,32.9,34.5,36.1,37.7,39.2,40.8,42.3,43.8,45.3/
C
C
      DATA NENTY/0/
C
C
C-- Define the logical unit for printout
C
      LOUT = IW(6)
C
#if defined(YDEBUG)
C
      NENTY=NENTY+1
C
      ICPOS=ICPOS+1
      NCPOS=NCPOS+1
C
      IF(IDUMPP(5).GE.1) THEN
        LDMP1=.TRUE.
      ELSE
        LDMP1=.FALSE.
      ENDIF
C
      IF(IDUMPP(6).GE.1) THEN
        LDMP2=.TRUE.
      ELSE
        LDMP2=.FALSE.
      ENDIF
C
      IDUMPP(5)=IDUMPP(5)-1
      IDUMPP(6)=IDUMPP(6)-1
C
      IF(LDMP1)
     &  WRITE(LOUT,7812) NENTY,IENT,MULIN,MNMUL,MXMUL,NEXCL,
     &  NVX,NHX,NEU,(IXHX(I),I=1,NHX),(IXNU(I),I=1,NEU)
 7812 FORMAT(' YVPOSS CALLED',I3,' TIMES. IENT=',I3,
     &  ' MULIN=',I3,' MNMUL=',I3,
     &  ' MXMUL=',I3,' NEXCL=',I3/ '  NVX:',I3,' NHX:',I3,' NEU:',I3,
     &  ' INDICES OF SEL. TRACKS:',20I3/(66X,20I3))
#endif
C
C     CALCULATION OF APPROXIMATIVE VERTEX?
      IF(NVX.GT.0) THEN
        LVAPC=.FALSE.
      ELSE
        LVAPC=.TRUE.
      ENDIF
C
      LCROS=.TRUE.
      IF(IENT.EQ.2) LCROS=.FALSE.
C
      MINMU=MNMUL
      MINMU=MAX0(MINMU,2)
C  RESHUFFLE INDICES OF TRACKS CONSIDERED IN VERTEX SEARCH
C  FIND HIGHEST TRACK INDEX USED
      NTRKS=0
      NPTR=0
      DO 50 I=1,NHX
        IF(IXHX(I).LE.0) GO TO 50
        NPTR=NPTR+1
        IPTR(NPTR)=IXHX(I)
        NTRKS=MAX0(NTRKS,IPTR(NPTR))
C     CHECK IF CROSSING TABLE HAS TO BE RECALCULATED
        IF(IPTR(NPTR).NE.IPTRO(NPTR)) LCROS=.TRUE.
        IPTRO(NPTR)=IPTR(NPTR)
   50 CONTINUE
      NPHX=NPTR
C
      DO 51 I=1,NEU
        IF(IXNU(I).LE.0) GO TO 51
        NPTR=NPTR+1
        IPTR(NPTR)=IXNU(I)+MAXHLX
        NTRKS=MAX0(NTRKS,IPTR(NPTR))
C     CHECK IF CROSSING TABLE HAS TO BE RECALCULATED
        IF(IPTR(NPTR).NE.IPTRO(NPTR)) LCROS=.TRUE.
        IPTRO(NPTR)=IPTR(NPTR)
   51 CONTINUE
C
      IF(NTRKS.LE.MAXTRK) GO TO 45
#if defined(YDEBUG)
      WRITE(LOUT,44) NTRKS,MAXTRK
   44 FORMAT(' ****** NTRKS=',I5,' ABOVE LIMIT',I5,' IN VXPOSS')
#endif
   45 CONTINUE
C
      IF(NTRKS.LT.MNMUL) THEN
        NSNGL=0
C-----
        NVPOSS=0
        RETURN
      ENDIF
C
C  INITIALIZE AND RESET TABLES
      DO 200 I=1,50
        NTRV00(I)=0
        CALL YMKZER(MKDIMM,NMSIZZ,ITRV00(1,I))
  200 CONTINUE
C
      NVPOSS=0
C
      IF(IENT.EQ.1) GO TO 1050
C
C =======  SET UP MARKERS FOR SETS OF TRACKS REQUIRED IN VERTEX =======
      II=1
      NSETR=JTREQ(1)
      IF(NSETR.LE.0) GO TO 700
      ISETR=0
C
      IF(NSETR.LT.10) GO TO 300
#if defined(YDEBUG)
      WRITE(LOUT,*) ' Error in YVPOSS JTREQ(1)= ',NSETR
#endif
C
      RETURN
C
  300 CONTINUE
      II=II+1
      LWORD=JTREQ(II)
      IF(LWORD.GE.0) GO TO 400
C  NEW SET
      ISETR=ISETR+1
      CALL YMKZER(MKDIMM,NMSIZZ,YMKST(1,ISETR))
      NTSET(ISETR)=0
C
      IF(ISETR.GT.NSETR) GO TO 500
C
      NTREQ(ISETR)=-LWORD
C     SET MIN. MULTIPLICITY TO MAX OF TRACKS REQ. IN A SUBSET
      MINMU =MAX0(MINMU ,NTREQ(ISETR))
      GO TO 300
C
  400 CONTINUE
      IF(LWORD.LE.MAXTRK) GO TO 450
C
#if defined(YDEBUG)
      WRITE(LOUT,410) II,LWORD
  410 FORMAT('   ERROR IN YVPOSS: JTREQ(',I4,')=',I10)
      WRITE(LOUT,420) (JTREQ(I),I=1,50)
  420 FORMAT(' JTREQ:', 25I3/7X,25I3)
#endif
C
      RETURN
C
  450 CONTINUE
      IF(LWORD.EQ.0) GO TO 300
      NTSET(ISETR)=NTSET(ISETR)+1
      CALL YMKSET(MKDIMM,NMSIZZ,YMKST(1,ISETR),LWORD)
      GO TO 300
  500 CONTINUE
C
C     CHECK FOR SUFFICIENT TRACKS IN EACH SET
      DO 590 ISETR=1,NSETR
        IF(NTREQ(ISETR).LE.NTSET(ISETR)) GO TO 590
C     NOT ENOUGH TRACKS IN SET
#if defined(YDEBUG)
        IF(LDMP2)
     1    WRITE(LOUT,550) ISETR,NTREQ(ISETR),NTSET(ISETR)
  550   FORMAT(/'  NOT ENOUGH TRACKS IN SET',I5,'   REQ.',I5,
     1    '  IN SET', I5)
#endif
        RETURN
C
  590 CONTINUE
C
  700 CONTINUE
C
#if defined(YDEBUG)
      IF(LDMP2) THEN
        WRITE(LOUT,7915) NSETR,(JTREQ(I),I=1,20)
 7915   FORMAT(/' YVPOSS: NSETR=',I5,' JTREQ',20I4)
        DO I=1,NSETR
          WRITE(LOUT,7916)
     2      I,NTREQ(I),NTSET(I),(YMKST(J,I),J=1,MKDIMM)
 7916     FORMAT(5X,I5,' NTREQ=',I8,'  NTSET=',I8,('  YMKST=',6Z11))
        ENDDO
      ENDIF
#endif
C
C ==== END SET UP MARKERS FOR SETS OF TRACKS REQUIRED IN VERTEX =======
C
 1050 CONTINUE
C
      NMWD=(NPTR-1)/NMSIZZ+1
C
      IF(LCROS) THEN
C
C ================== SET UP CROSS TABLE  ==============================
        DO 100 I=1,MAXTRK
          CALL YMKZER(MKDIMM,NMSIZZ,IJCRS(1,I))
          NJCRS(I)=0
  100   CONTINUE
C
C
        NPTM1=NPTR-1
        DO 1800 I=1,NPTM1
          NH=0
          NU=0
          I1=I+1
          II=IPTR(I)
          IF(II.GT.MAXHLX) THEN
            NU=NU+1
            IXNUU(NU)=II-MAXHLX
          ELSE
            NH=NH+1
            IXHXX(NH)=II
          ENDIF
          CALL YMKSET(NMWD,NMSIZZ,IJCRS(1,I),I)
C
          NHO=NH
          NUO=NU
          DO 1700 J=I1,NPTR
            NH=NHO
            NU=NUO
C
            JJ=IPTR(J)
            IF(JJ.GT.MAXHLX) THEN
              NU=NU+1
              IXNUU(NU)=JJ-MAXHLX
            ELSE
              NH=NH+1
              IXHXX(NH)=JJ
            ENDIF
C
            LVAPP=LVAPC
            IF(NH.LT.2) LVAPP=.FALSE.
            CALL YFTVTR(NVX,NH,NU,LVAPP,VXIN,VVXIN,
     &        IXHXX,NSHX,NSVHX,HXI,VHXI,
     &        IXNUU,NSNU,NSVNU,TNU,VTNU,
     &        VTX2,VARV2,CHVX2,IFAIL)
C
            IF(IFAIL.NE.0) GO TO 1600
            NDF=2*(NH+NU)+3*(NVX-1)
            IF(CHVX2.GT.CONLM(NDF)) GO TO 1600
            CALL YMKSET(NMWD,NMSIZZ,IJCRS(1,I),J)
            NJCRS(I)=NJCRS(I)+1
            CALL YMKSET(NMWD,NMSIZZ,IJCRS(1,J),I)
            NJCRS(J)=NJCRS(J)+1
 1600       CONTINUE
 1700     CONTINUE
 1800   CONTINUE
        CALL YMKSET(NMWD,NMSIZZ,IJCRS(1,NPTR),NPTR)
C
C ++++++ OPTIONAL WRITE(LOUT,OF CROSSING TABLES
#if defined(YDEBUG)
        IF(LDMP1) THEN
          WRITE(LOUT,7110)
 7110     FORMAT(/'     TRACK CROSSING TABLE')
          DO 7150 I=1,NPTR
            DO 7050 J=1,NPTR
              ITEMP(J)=0
              CALL YMKTST(NMWD,NMSIZZ,IJCRS(1,I),J,LMRK)
              IF(LMRK) THEN
                ITEMP(J)=IPTR(J)
              ENDIF
C
 7050       CONTINUE
            WRITE(LOUT,7100) IJCRS(1,I),I,IPTR(I),NJCRS(I),
     &        (ITEMP(J),J=1,NPTR)
 7100       FORMAT(5X,Z9,I5,I5,I7,3X,6(1X,5I3)/
     &        (34X,1X,5I3,1X,5I3,1X,5I3,1X,5I3,1X,5I3,1X,5I3))
 7150     CONTINUE
        ENDIF
#endif
C
C  SEARCH FOR TRKS WITHOUT CROSSINGS; SHRINK ARRAYS AND CROSSING
C  TABLE
C
        NSNGL=0
 1900   CONTINUE
        NMWD0=(NTRKS-1)/NMSIZZ+1
        NMWD=(NPTR-1)/NMSIZZ+1
C
        DO 1950 KT=1,NPTR
          CALL YMKCNT(NMWD,NMSIZZ,NPTR,IJCRS(1,KT),N)
          IF(N.LE.1) GO TO 1920
C
          IF(NSETR.LE.0) GO TO 1950
C
          IF(IENT.EQ.1) GO TO 1950
C
C     REQUIRE CROSSING WITH AT LEAST ONE TRACK OF EVERY SUBSET
          DO 1910 ISETR=1,NSETR
C     JCOMB=0
            CALL YMKZER(NMWD0,NMSIZZ,JCOMB(1))
C
            DO 1905 I=1,NPTR
              CALL YMKTST(NMWD,NMSIZZ,IJCRS(1,KT),I,LMRK)
              IF(LMRK) THEN
                CALL YMKSET(NMWD0,NMSIZZ,JCOMB(1),IPTR(I))
              ENDIF
C
 1905       CONTINUE
C
C     IF(IAND(JCOMB     ,YMKST(ISETR)).EQ.0) GO TO 1920
            CALL YMKAND(NMWD0,NMSIZZ,JCOMB(1),YMKST(1,ISETR),JCOMB(1),
     &        LZERO)
            IF(LZERO) THEN
C     NO TRACK OF REQUIRED SET
              GO TO 1920
            ENDIF
            CALL YMKCNT(NMWD0,NMSIZZ,NTRKS,JCOMB(1),N)
            IF(N.GE.NTREQ(ISETR)) GO TO 1910
C     TOO FEW TRACKS OF REQUIRED SET
            GO TO 1920
C
 1910     CONTINUE
          GO TO 1950
C
 1920     NSNGL=NSNGL+1
          KSNGL(NSNGL)=IPTR(KT)
C     NON CROSSING TRACK FOUND : SHRINK
          KTR=KT

          NPTR=NPTR-1
          DO 1960 IT=KTR,NPTR
            IPTR(IT)=IPTR(IT+1)
            CALL YMKCOP(MKDIMM,IJCRS(1,IT+1),IJCRS(1,IT))
 1960     CONTINUE
          DO 1970 JT=1,NPTR
            IPTRO(JT)=IPTR(JT)
            CALL YMKSHR(NMWD,NMSIZZ,IJCRS(1,JT),KTR)
 1970     CONTINUE
C
          GO TO 1900
C
 1950   CONTINUE
C
 1980   CONTINUE
C
        IF(NSNGL.LE.0) GO TO 1990
C
C  RECALCULATE NUMBER OF CROSSINGS
        DO 1985 I=1,NPTR
          CALL YMKCNT(NMWD,NMSIZZ,NPTR,IJCRS(1,I),NJCRS(I))
          NJCRS(I)=NJCRS(I)-1
 1985   CONTINUE
C
#if defined(YDEBUG)
        IF(.NOT.LDMP1) GO TO 1990
        WRITE(LOUT,8010) NSNGL,(KSNGL(I),I=1,NSNGL)
 8010   FORMAT(' YVPOSS:' ,I5,' NON CROSS.TRKS REMOVED:',31I3)
        DO 8150 I=1,NPTR
          DO 8050 J=1,NPTR
            ITEMP(J)=0
            CALL YMKTST(NMWD,NMSIZZ,IJCRS(1,I),J,LMRK)
            IF(LMRK) THEN
              ITEMP(J)=IPTR(J)
            ENDIF
C
 8050     CONTINUE
          IF(I.EQ. 1) WRITE(LOUT,7110)
          WRITE(LOUT,7100) IJCRS(1,I),I,IPTR(I),NJCRS(I),
     &      (ITEMP(J),J=1,NPTR)
 8150   CONTINUE
C
#endif
 1990   CONTINUE
C ============== END SET UP CROSS TABLE  ==============================
C
      ENDIF
C
      IF(IENT.EQ.1) RETURN
C
C
C*********************
C
C
C  SEARCH FOR ALL VERTEX POSSIBILITIES
C
      NVPOSS=0
C
C CHECK IF ENOUGH TRACKS ARE PRESENT FOR REQ. VTX MULT.
      IF(NPTR.LT.MINMU) RETURN
C
C  LOOP OVER ALL TRACK COMBINATIONS, REDUCING IN EACH STEP THE
C  NUMBER OF TRACKS IN VERTEX BY ONE. STOP IF ALL COMBINATIONS
C  WITH A GIVEN NUMBER OF TRACKS LEAD TO A VERTEX
C
      MAXMU=MXMUL
      IF(MAXMU.LT.2.OR.MAXMU.GT.NPTR) MAXMU=NPTR
C     REDUCE MAXMU IF NOT ENOUGH TRACKS WITH SUFFICIENT CROSSINGS
 2020 CONTINUE
      NNTR=0
      DO 2030 I=1,NPTR
        ITR=IPTR(I)
        IF(NJCRS(I).GE.(MAXMU-1)) THEN
          NNTR=NNTR+1
        ENDIF
 2030 CONTINUE
      IF(NNTR.LT.MAXMU) THEN
        MAXMU=MAXMU-1
        GO TO 2020
      ENDIF
C
      IMIS1=NPTR-MAXMU
      IMIS2=NPTR-MINMU
      DO 4000 IMIST=IMIS1,IMIS2
#if defined(YDEBUG)
        IF(LDMP2)
     &    WRITE(LOUT,9199) IMIST,IMIS1,NPTR,NVPOSS,MULIN,NTRV00(1)
 9199   FORMAT(' IMIST,IMIS1,NPTR',3I5,'  NVPOSS,MULIN,NTRV00(1)',3I5)
#endif
        IF( NVPOSS .LE. 0 .OR. MULIN .LT. 0 ) GOTO 2050
C     STOP SEARCH FOR LOW MULTIPLICITY VERTICES?
        IF((NPTR-IMIST).LE.(NTRV00(1)-MULIN)) GO TO 5000
 2050   CONTINUE
        IREQ=NPTR-IMIST
C
C     SELECT TRACKS WITH SUFFICIENT NUMBER OF CROSSINGS
        NJPTR=0
C
        CALL YMKZER(NMWD,NMSIZZ,MMSET(1))
C
        DO 2070 KTR=1,NPTR
          IF(NJCRS(KTR).GE.(IREQ-1)) THEN
            NJPTR=NJPTR+1
            IJTR(NJPTR)=KTR
            CALL YMKSET(NMWD,NMSIZZ,MMSET(1),KTR)
          ENDIF
 2070   CONTINUE
C
C     REQUIRE SUFFICIENT CROSSINGS WITHIN SET
 2080   CONTINUE
#if defined(YDEBUG)
        IF(LDMP2) THEN
          WRITE(LOUT,8897) NJPTR,IREQ,MMSET(1)
 8897     FORMAT( ' YVPOSS; NJPTR,IREQ:',2I5,'  MMSET=',Z12)
        ENDIF
#endif
C
        IF(NJPTR.LT.IREQ) THEN
C     TOO FEW TRACKS WITH SUFFICIENT CROSSINGS
          GO TO 4000
        ENDIF
C
        DO 2090 JTR=1,NJPTR
          CALL YMKIDC(NMWD,NMSIZZ,NPTR,IJCRS(1,IJTR(JTR)),MMSET(1),NN)
          IF(NN.LT.IREQ) THEN
C     REMOVE TRACK FROM REDUCED SET
            NJPTR=NJPTR-1
            CALL YMKCLE(NMWD,NMSIZZ,MMSET(1),IJTR(JTR))
            DO 2085 KTR=JTR,NJPTR
              IJTR(KTR)=IJTR(KTR+1)
 2085       CONTINUE
            GO TO 2080
          ENDIF
C
 2090   CONTINUE
C
        JJTR(1)=0
        LCOMB=.TRUE.
 2100   CONTINUE
        CALL COMBI(JJTR,NJPTR,IREQ)
#if defined(YDEBUG)
        IF(LDMP2)
     $    WRITE(LOUT,5421) NPTR,IREQ,(IJTR(JJTR(I)),JJTR(I),I=1,IREQ)
 5421   FORMAT(6H COMBI,2I3,2X,10(I4,'(',I3,')')/14X,10(I4,'(',I3,')'))
#endif
C     COMBINATIONS EXHAUSTED?
        IF(JJTR(1).EQ.0) GO TO 3800
        DO 2095 IR=1,IREQ
          IITR(IR)=IJTR(JJTR(IR))
 2095   CONTINUE
C     MARKER FOR USED TRACKS
        CALL YMKZER(MKDIMM,NMSIZZ,MCOMB(1))
        CALL YMKZER(MKDIMM,NMSIZZ,JCOMB(1))
        DO 2200 I=1,IREQ
          CALL YMKSET(NMWD,NMSIZZ,MCOMB(1),IITR(I))
          CALL YMKSET(NMWD0,NMSIZZ,JCOMB(1),IPTR(IITR(I)))
C
 2200   CONTINUE
C
C     AVOID EXCLUDED TRACK COMBINATIONS
        IF(NEXCL.LE.0) GO TO 2250
        DO 2240 IEXCL=1,NEXCL
          CALL YMKIDT(NMWD0,NMSIZZ,JCOMB(1),MRKEXC(1,IEXCL),LMRK)
          IF(LMRK) GO TO 3500
 2240   CONTINUE
 2250   CONTINUE
C
C     CHECK IF SAME COMBINATION WAS ALREADY SUCCESSFUL
        IF(NVPOSS.LE.0) GO TO 2500
        DO 2400 IVP=1,NVPOSS
#if defined(YDEBUG)
          IF(LDMP2)
     $      WRITE(LOUT,3314) IVP,NVPOSS,JCOMB(1),ITRV00(1,IVP)
 3314     FORMAT(5H IVP=,I5,9H  NVPOSS=,I5,2Z10)
#endif
          CALL YMKMIS(NMWD0,NMSIZZ,JCOMB(1),ITRV00(1,IVP),LMRK)
          IF(.NOT.LMRK) GO TO 3400
C
 2400   CONTINUE
 2500   CONTINUE
C
C  CHECK IF REQUIRED TRACKS ARE CONTAINED IN VERTEX SET
        IF(NSETR.LE.0) GO TO 2550
        DO 2530 ISETR=1,NSETR
          CALL YMKIDC(NMWD0,NMSIZZ,NTRKS,JCOMB(1),YMKST(1,ISETR),NN)
#if defined(YDEBUG)
          IF(LDMP2) THEN
            WRITE(LOUT,9773) JCOMB(1),YMKST(1,ISETR),NN
 9773       FORMAT( ' YVPOSS CALLED YMKIDC',2Z12,I5)
          ENDIF
#endif
          IF(NN.LT.NTREQ(ISETR)) THEN
C     NOT ENOUGH TRACKS FROM SET
            GO TO 3300
          ENDIF
C
 2530   CONTINUE
C
 2550   CONTINUE
C
C
C  CHECK IF ALL COMBINATIONS OF PAIRS CROSS
        DO 2600 IR=1,IREQ
          II=IITR(IR)
          CALL YMKMIS(NMWD,NMSIZZ,MCOMB(1),IJCRS(1,II),LMRK)
#if defined(YDEBUG)
          IF(LDMP2) THEN
            WRITE(LOUT,9774) MCOMB(1),IJCRS(1,II),LMRK
 9774       FORMAT( ' YVPOSS CALLED YMKMIS',2Z12,L5)
          ENDIF
#endif
          IF(LMRK) THEN
C     NOT ALL PAIRS CROSSING
            GO TO 3200
          ENDIF
C
 2600   CONTINUE
C
C  LOOK FOR VERTEX OF SELECTED TRACKS
C
        NH=0
        NU=0
        DO 2800 IR=1,IREQ
          II=IITR(IR)
          ITR=IPTR(II)
          IF(ITR.GT.MAXHLX) THEN
            NU=NU+1
            IXNUU(NU)=ITR-MAXHLX
          ELSE
            NH=NH+1
            IXHXX(NH)=ITR
          ENDIF
#if defined(YDEBUG)
          IF(LDMP2) THEN
            WRITE(LOUT,2720) IR,IREQ,II,ITR,NH,IXHXX(NH),NU,IXNUU(NU)
 2720       FORMAT(' YVPOSS: PAIRS CROSSING, VERTEX?'/5X,
     &   ' IR,IREQ,II,ITR',4I3,' NH,IXHXX(NH)',2I3,' NU,IXNUU(NU)',2I3)
          ENDIF
#endif
C
 2800   CONTINUE
        NV=NVPOSS+1
C
        LVAPP=LVAPC
        IF(NH.LT.2) LVAPP=.FALSE.
        CALL YFTVTR(NVX,NH,NU,LVAPP,VXIN,VVXIN,
     &    IXHXX,NSHX,NSVHX,HXI,VHXI,
     &    IXNUU,NSNU,NSVNU,TNU,VTNU,
     &    VTXMS0(1,NV),VARVMS(1,NV),CHIVMS(NV),IFAIL)
C
C  CHECK IF VERTEX IS GOOD: TOTAL CHISQ, CCC MAX. CHISQ OF TRACK
        NDF=2*IREQ+3*(NVX-1)
CGEL*****======= TEMPORARY CODING
        IF(NDF.GT.20) GO TO 2860
        XILIM=CONLM(NDF)
        GO TO 2870
 2860   CONTINUE
        XILIM=2.25*NDF
 2870   CONTINUE
C
        IF(IFAIL.NE.0) GO TO 3000
        IF(CHIVMS(NV).GT.XILIM) GO TO 3000
C  VERTEX FOUND
        NVPOSS=NVPOSS+1
        NTRV00(NVPOSS)=IREQ
        CALL YMKCOP(MKDIMM,JCOMB(1),ITRV00(1,NVPOSS))
C
C  TOO MANY VERTICES FOUND
        IF( NVPOSS .GE. 50 ) GO TO 5000
C
        GO TO 2100
C
C
C  FAILURES IN VERTEX SEARCH
 3000   CONTINUE
C  TOTAL CHISQ OF VERTEX TOO HIGH
#if defined(YDEBUG)
        IF( LDMP2 )
     $    WRITE(LOUT,1583) NDF,XILIM,CHIVMS(NV),
     &    (IITR(K),K=1,IREQ)
 1583   FORMAT(26H TOT.CHISQ OF VTX TOO HIGH, I5,2F10.2,5X,31I3)
#endif
        GO TO 3300
 3200   CONTINUE
C     NOT ALL PAIRS CROSSING
#if defined(YDEBUG)
        IF(LDMP2) THEN
          WRITE(LOUT,8912) IR,II,IPTR(II),IJCRS(1,IR),MCOMB(1)
 8912     FORMAT(' NOT ALL PAIRS CROSSING, IR,II,IPTR:',3I5,
     &      ' IJCRS(1,IR),MCOMB:',2Z10)
        ENDIF
#endif
        GO TO 3300
C
 3500   CONTINUE
C     VERTEX EXCLUDED BY PHYSICS REASONING
#if defined(YDEBUG)
        IF(LDMP2)
     &    WRITE(LOUT,3510) JCOMB(1),NEXCL,IEXCL,MRKEXC(1,IEXCL)
 3510   FORMAT(' EXCL.COMB. JCOMB=',Z10,' NEXCL,',2I5,
     &    '  MRKEXC=',Z10)
#endif
        GO TO 3300
C
 3300   CONTINUE
        LCOMB=.FALSE.
        GO TO 2100
 3400   CONTINUE
C  COMBINATION ALREADY SUCCESSFUL
        GO TO 2100
C
 3800   CONTINUE
C     ALL COMBINATIONS WITH SAME NUMBER OF TRACKS COMPLETED
CXCX correction 14/12/89 G.Lutz
        IF(LCOMB) THEN
C     All combinations of tracks have lead to vertices
C     exclude tracks from further search if they cross only
C     with oter tracks of the considered sample
          DO 3900 JTR=1,NJPTR
            KTR=IJTR(JTR)
            CALL YMKMIS(NMWD,NMSIZZ,IJCRS(KTR,1),MMSET(1),LMRK)
            IF(LMRK) THEN
C     track accepted
            ELSE
C     reject track from further search
              NJCRS(KTR)=0
            ENDIF
 3900     CONTINUE
        ENDIF
 4000 CONTINUE
 5000 CONTINUE
C
#if defined(YDEBUG)
C
      IF(.NOT.LDMP1) RETURN
      WRITE(LOUT,7200) NVPOSS
 7200 FORMAT(I4,' VERTEX POSSIBILITIES.'/'   # TRACKS   X       Y',
     & '  Z      RES.X   RES.Y   RES.Z   CHISQ TRACKS USED')
      DO  7300 I=1,NVPOSS
        DO 7250 J=1,NPTR
          ITEMP(J)=0
          CALL YMKTST(NMWD0,NMSIZZ,ITRV00(1,I),IPTR(J),LMRK)
          IF(LMRK) THEN
            ITEMP(J)=IPTR(J)
          ENDIF
 7250   CONTINUE
        WRITE(LOUT,7270) I,NTRV00(I),(VTXMS0(J,I),J=1,3),
     &    SQRT(VARVMS(1,I)),SQRT(VARVMS(3,I)),SQRT(VARVMS(6,I)),
     &    CHIVMS(I),(ITEMP(J),J=1,NPTR)
 7270   FORMAT(I4,I5,2F8.3,F10.3,4F8.3,(30I2))
 7300 CONTINUE
C
#endif
      RETURN
      END
#endif
